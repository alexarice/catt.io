#
#  cyl-vert.catt - vertical composition of cylinders
#

import base

#
#  Vertical composition of two 1-cylinders
#

section
  (x0 : *) (x1 : *) (x01 : x0 -> x1)
  (y0 : *) (y1 : *) (y01 : y0 -> y1)
  (f0 : x0 -> y0) (f1 : x1 -> y1) (f01 : comp(f0,y01) -> comp(x01,f1))
  (x2 : *) (x12 : x1 -> x2)
  (y2 : *) (y12 : y1 -> y2)
  (f2 : x2 -> y2) (f12 : comp(f1,y12) -> comp(x12,f2)) where

  let v1x : x0 -> x2 = comp(x01,x12)
  let v1y : y0 -> y2 = comp(y01,y12)

  # assoc
  let v1_coh1 : comp(f0,comp(y01,y12)) ->
             comp(comp(f0,y01),y12)
    = assoc_l(f0,y01,y12)

  # f01
  let v1_coh2 : comp(comp(f0,y01),y12) ->
             comp(comp(x01,f1),y12)
    = whisk_r(f01,y12)

  # assoc
  let v1_coh3 : comp(comp(x01,f1),y12) ->
             comp(x01,comp(f1,y12))
    = assoc_r(x01,f1,y12)
    
  # f12 
  let v1_coh4 : comp(x01,comp(f1,y12)) ->
             comp(x01,comp(x12,f2))
    = whisk_l(x01,f12)

  # assoc
  let v1_coh5 : comp(x01,comp(x12,f2)) -> 
             comp(comp(x01,x12),f2)
    = assoc_l(x01,x12,f2)
             
  let v1f : comp(f0,comp(y01,y12)) -> comp(comp(x01,x12),f2)
    = vert5(v1_coh1(),v1_coh2(),v1_coh3(),v1_coh4(),v1_coh5())

end

#
#  Some coherences
#

coh whisk_r_comp
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
    (w : *) (k : z -> w)
  : vert(whisk_r(a,comp(h,k)),assoc_l(g,h,k)) ->
    vert(assoc_l(f,h,k),whisk_r(whisk_r(a,h),k))

coh whisk_r_fuse
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
            (h : x -> y) (b : g -> h)
    (z : *) (k : y -> z)
  : vert(whisk_r(a,k),whisk_r(b,k)) ->
    whisk_r(vert(a,b),k)

coh whisk_r_unfuse
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
            (h : x -> y) (b : g -> h)
    (z : *) (k : y -> z)
  : whisk_r(vert(a,b),k) ->
    vert(whisk_r(a,k),whisk_r(b,k))

coh whisk_l_comp
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (w : *) (h : z -> w)
            (k : z -> w) (a : h -> k)
  : vert(whisk_l(f,whisk_l(g,a)),assoc_l(f,g,k)) ->
    vert(assoc_l(f,g,h),whisk_l(comp(f,g),a))

coh whisk_l_fuse
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
            (h : y -> z) (a : g -> h)
            (k : y -> z) (b : h -> k)
  : vert(whisk_l(f,a),whisk_l(f,b)) ->
    whisk_l(f,vert(a,b))

coh whisk_l_unfuse
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
            (h : y -> z) (a : g -> h)
            (k : y -> z) (b : h -> k)
  : whisk_l(f,vert(a,b)) ->
    vert(whisk_l(f,a),whisk_l(f,b)) 

coh whisk_lr
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
            (h : y -> z) (a : g -> h)
    (w : *) (k : z -> w)
  : vert(whisk_r(whisk_l(f,a),k),assoc_r(f,h,k)) ->
    vert(assoc_r(f,g,k),whisk_l(f,whisk_r(a,k)))

#
#  Vertical composition of two 2-cylinders
#

section
  (x0 : *) (x1 : *) (x01 : x0 -> x1)
  (y0 : *) (y1 : *) (y01 : y0 -> y1)
  (f0 : x0 -> y0) (f1 : x1 -> y1) (f01 : comp(f0,y01) -> comp(x01,f1))
  (g0 : x0 -> y0) (g1 : x1 -> y1) (g01 : comp(g0,y01) -> comp(x01,g1))
  (a0 : f0 -> g0) (a1 : f1 -> g1) (a01 : vert(whisk_r(a0,y01),g01) -> vert(f01,whisk_l(x01,a1)))

  (x2 : *) (x12 : x1 -> x2)
  (y2 : *) (y12 : y1 -> y2)
  (f2 : x2 -> y2) (f12 : comp(f1,y12) -> comp(x12,f2))
  (g2 : x2 -> y2) (g12 : comp(g1,y12) -> comp(x12,g2))
  (a2 : f2 -> g2) (a12 : vert(whisk_r(a1,y12),g12) -> vert(f12,whisk_l(x12,a2))) where

  let x : x0 -> x2 = comp(x01,x12)
  let y : y0 -> y2 = comp(y01,y12)

  let f : comp(f0,y()) -> comp(x(),f2)
    = v1f(x0,x1,x01,y0,y1,y01,f0,f1,f01,x2,x12,y2,y12,f2,f12)

  let g : comp(g0,y()) -> comp(x(),g2)
    = v1f(x0,x1,x01,y0,y1,y01,g0,g1,g01,x2,x12,y2,y12,g2,g12)

  # assoc
  sig coh1 : vert(whisk_r(a0,comp(y01,y12)),vert5(assoc_l(g0,y01,y12),whisk_r(g01,y12),assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
             vert(vert(whisk_r(a0,comp(y01,y12)),assoc_l(g0,y01,y12)),vert4(whisk_r(g01,y12),assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2)))

  # whisk_r_comp
  sig coh2 : vert(vert(whisk_r(a0,comp(y01,y12)),assoc_l(g0,y01,y12)),vert4(whisk_r(g01,y12),assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
             vert(vert(assoc_l(f0,y01,y12),whisk_r(whisk_r(a0,y01),y12)),vert4(whisk_r(g01,y12),assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2)))

  # assoc
  sig coh3 : vert(vert(assoc_l(f0,y01,y12),whisk_r(whisk_r(a0,y01),y12)),vert4(whisk_r(g01,y12),assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
             vert3(assoc_l(f0,y01,y12),vert(whisk_r(whisk_r(a0,y01),y12),whisk_r(g01,y12)),vert3(assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2)))

  # whisk_r_fuse
  sig coh4 : vert3(assoc_l(f0,y01,y12),vert(whisk_r(whisk_r(a0,y01),y12),whisk_r(g01,y12)),vert3(assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
             vert3(assoc_l(f0,y01,y12),whisk_r(vert(whisk_r(a0,y01),g01),y12),vert3(assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2))) 

  # a01
  sig coh5 : vert3(assoc_l(f0,y01,y12),whisk_r(vert(whisk_r(a0,y01),g01),y12),vert3(assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
             vert3(assoc_l(f0,y01,y12),whisk_r(vert(f01,whisk_l(x01,a1)),y12),vert3(assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2)))
             
  # whisk_r_unfuse
  sig coh6 : vert3(assoc_l(f0,y01,y12),whisk_r(vert(f01,whisk_l(x01,a1)),y12),vert3(assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
             vert3(assoc_l(f0,y01,y12),vert(whisk_r(f01,y12),whisk_r(whisk_l(x01,a1),y12)),vert3(assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2)))

  # assoc
  sig coh7 : vert3(assoc_l(f0,y01,y12),vert(whisk_r(f01,y12),whisk_r(whisk_l(x01,a1),y12)),vert3(assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
             vert3(vert(assoc_l(f0,y01,y12),whisk_r(f01,y12)),vert(whisk_r(whisk_l(x01,a1),y12),assoc_r(x01,g1,y12)),vert(whisk_l(x01,g12),assoc_l(x01,x12,g2)))

  # assoc
  sig coh8 : vert3(vert(assoc_l(f0,y01,y12),whisk_r(f01,y12)),vert(whisk_r(whisk_l(x01,a1),y12),assoc_r(x01,g1,y12)),vert(whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
             vert3(vert(assoc_l(f0,y01,y12),whisk_r(f01,y12)),vert(whisk_r(whisk_l(x01,a1),y12),assoc_r(x01,g1,y12)),vert(whisk_l(x01,g12),assoc_l(x01,x12,g2))) 

  # whisk_lr
  sig coh9 : vert3(vert(assoc_l(f0,y01,y12),whisk_r(f01,y12)),vert(whisk_r(whisk_l(x01,a1),y12),assoc_r(x01,g1,y12)),vert(whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
             vert3(vert(assoc_l(f0,y01,y12),whisk_r(f01,y12)),vert(assoc_r(x01,f1,y12),whisk_l(x01,whisk_r(a1,y12))),vert(whisk_l(x01,g12),assoc_l(x01,x12,g2)))
             
  # assoc
  sig coh10 : vert3(vert(assoc_l(f0,y01,y12),whisk_r(f01,y12)),vert(assoc_r(x01,f1,y12),whisk_l(x01,whisk_r(a1,y12))),vert(whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
              vert3(vert3(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12)),vert(whisk_l(x01,whisk_r(a1,y12)),whisk_l(x01,g12)),assoc_l(x01,x12,g2))

  # whisk_l_fuse
  sig coh11 : vert3(vert3(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12)),vert(whisk_l(x01,whisk_r(a1,y12)),whisk_l(x01,g12)),assoc_l(x01,x12,g2)) ->
              vert3(vert3(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12)),whisk_l(x01,vert(whisk_r(a1,y12),g12)),assoc_l(x01,x12,g2))

  # a12
  sig coh12 : vert3(vert3(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12)),whisk_l(x01,vert(whisk_r(a1,y12),g12)),assoc_l(x01,x12,g2)) ->
              vert3(vert3(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12)),whisk_l(x01,vert(f12,whisk_l(x12,a2))),assoc_l(x01,x12,g2))

  # whisk_l_unfuse
  sig coh13 : vert3(vert3(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12)),whisk_l(x01,vert(f12,whisk_l(x12,a2))),assoc_l(x01,x12,g2)) ->
              vert3(vert3(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12)),vert(whisk_l(x01,f12),whisk_l(x01,whisk_l(x12,a2))),assoc_l(x01,x12,g2))

  # assoc
  sig coh14 : vert3(vert3(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12)),vert(whisk_l(x01,f12),whisk_l(x01,whisk_l(x12,a2))),assoc_l(x01,x12,g2)) -> 
              vert(vert4(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12),whisk_l(x01,f12)),vert(whisk_l(x01,whisk_l(x12,a2)),assoc_l(x01,x12,g2)))

  # whisk_l_comp
  sig coh15 : vert(vert4(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12),whisk_l(x01,f12)),vert(whisk_l(x01,whisk_l(x12,a2)),assoc_l(x01,x12,g2))) ->
              vert(vert4(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12),whisk_l(x01,f12)),vert(assoc_l(x01,x12,f2),whisk_l(comp(x01,x12),a2)))

  # assoc
  sig coh16 : vert(vert4(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12),whisk_l(x01,f12)),vert(assoc_l(x01,x12,f2),whisk_l(comp(x01,x12),a2))) ->
              vert(vert5(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12),whisk_l(x01,f12),assoc_l(x01,x12,f2)),whisk_l(comp(x01,x12),a2))

  # (unfolded) final goal
  sig a : vert(whisk_r(a0,comp(y01,y12)),vert5(assoc_l(g0,y01,y12),whisk_r(g01,y12),assoc_r(x01,g1,y12),whisk_l(x01,g12),assoc_l(x01,x12,g2))) ->
          vert(vert5(assoc_l(f0,y01,y12),whisk_r(f01,y12),assoc_r(x01,f1,y12),whisk_l(x01,f12),assoc_l(x01,x12,f2)),whisk_l(comp(x01,x12),a2))

end
