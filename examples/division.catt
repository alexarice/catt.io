#
#  division.catt - working on left division
#

import prelude

coh whisk_l_comp
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (w : *) (h : z -> w)
            (k : z -> w) (a : h -> k)
  : comp221(comp120(f,comp120(g,a)),assoc_l(f,g,k)) ->
    comp221(assoc_l(f,g,h),comp120(comp110(f,g),a))

coh ichg21
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
            (k : y -> z) (b : h -> k)
  : comp221(comp210(a,h),comp120(g,b)) ->
    comp221(comp120(f,b),comp210(a,k))

coh ichg21_inv
    (x : *)
    (y : *) (f : x -> y)
            (g : x -> y) (a : f -> g)
    (z : *) (h : y -> z)
            (k : y -> z) (b : h -> k)
  : comp221(comp120(f,b),comp210(a,k)) -> 
    comp221(comp210(a,h),comp120(g,b)) 

coh big_coh
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (w : *) (h : z -> w)
            (k : z -> w) (a : h -> k)
  : comp120(comp110(f,g),a) ->
    comp221(assoc_r(f,g,h),comp221(comp120(f,comp120(g,a)),assoc_l(f,g,k)))

coh big_coh_inv
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (w : *) (h : z -> w)
            (k : z -> w) (a : h -> k)
  : comp221(assoc_r(f,g,h),comp221(comp120(f,comp120(g,a)),assoc_l(f,g,k))) ->
    comp120(comp110(f,g),a) 

section
  (x : *) (y : *) (z : *)
  
  (u : x -> y) (ui : y -> x)
  (r0 : comp110(ui,u) -> id(y))
  (r1 : comp110(u,ui) -> id(x))
  
  (v0 : y -> z) (v1 : y -> z)  
  (s : v0 -> v1)
  (t : v0 -> v1)
  (w : comp120(u,s) -> comp120(u,t))
  where   

  let bigstep : comp221(comp120(comp110(ui,u),s),comp210(r0,v1)) ->
                comp221(comp221(assoc_r(ui,u,v0),comp221(comp120(ui,comp120(u,s)),assoc_l(ui,u,v1))),comp210(r0,v1))
              = comp321(big_coh(ui,u,s),comp210(r0,v1))
              
  let using_w : comp221(comp221(assoc_r(ui,u,v0),comp221(comp120(ui,comp120(u,s)),assoc_l(ui,u,v1))),comp210(r0,v1)) ->
                comp221(comp221(assoc_r(ui,u,v0),comp221(comp120(ui,comp120(u,t)),assoc_l(ui,u,v1))),comp210(r0,v1))
              = comp321(comp231(assoc_r(ui,u,v0),comp321(comp130(ui,w),assoc_l(ui,u,v1))),comp210(r0,v1))

  let bigstep_inv : comp221(comp221(assoc_r(ui,u,v0),comp221(comp120(ui,comp120(u,t)),assoc_l(ui,u,v1))),comp210(r0,v1)) ->
                    comp221(comp120(comp110(ui,u),t),comp210(r0,v1))
                  = comp321(big_coh_inv(ui,u,t),comp210(r0,v1))

  let w1 : comp221(comp120(comp110(ui,u),s),comp210(r0,v1)) ->
           comp221(comp120(comp110(ui,u),t),comp210(r0,v1))
         = comp332(comp332(bigstep(),using_w()),bigstep_inv())

  # the interchangers ...
  
  let coh_ichg_up : comp221(comp210(r0,v0),comp120(id(y),s)) ->
                    comp221(comp120(comp110(ui,u),s),comp210(r0,v1))
                  = ichg21(r0,s)

  let coh_ichg_down : comp221(comp120(comp110(ui,u),t),comp210(r0,v1)) -> 
                      comp221(comp210(r0,v0),comp120(id(y),t)) 
                    = ichg21_inv(r0,t)

  let finally : comp221(comp210(r0,v0),comp120(id(y),s)) -> 
                comp221(comp210(r0,v0),comp120(id(y),t))
              = comp332(comp332(coh_ichg_up(),w1()),coh_ichg_down())
             
end
